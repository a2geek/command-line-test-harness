plugins {
    id 'org.springframework.boot' version "4.0.1"
    id 'java'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.3'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "info.picocli:picocli:4.7.7"
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
    implementation project(":lib")

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': "Command Line Test Harness 'clth'",
                   'Implementation-Version': parent.version
    }
}

application {
    mainClass = 'io.github.a2geek.clth.app.Main'
}

// Disable all Spring AOT processing
processAot.configure {
    enabled = false;
}
processTestAot.configure {
    enabled = false;
}

bootJar {
    archiveBaseName = 'clth'
    manifest {
        attributes 'Implementation-Title': "Command Line Test Harness 'clth'",
                   'Implementation-Version': parent.version
    }
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("clth-${osName}-${osArch}-${parent.version}")
        }
    }
}
